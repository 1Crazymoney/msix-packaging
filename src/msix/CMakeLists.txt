# MSIX\src\msix
# Copyright (C) 2017 Microsoft.  All rights reserved.
# See LICENSE file in the project root for full license information.

cmake_minimum_required(VERSION 3.1.0 FATAL_ERROR)
set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} ${CMAKE_CURRENT_SOURCE_DIR}/../../cmake) # main (top) cmake dir

set(LIBRARY_NAME msix)

project(${LIBRARY_NAME})

# Define PALs
IF(WIN32)
	set (MSIX_API=1)
    set (DirectoryObject PAL/FileSystem/Win32/DirectoryObject.cpp)
    set (SHA256 PAL/SHA256/Win32/SHA256.cpp)
    set (Signature PAL/Signature/Win32/SignatureValidator.cpp)
ELSE()
    # Visibility variables for non-win32 platforms
    IF((IOS) OR (MACOS))
        # on Apple platforms you can explicitly define which symbols are exported
        set(CMAKE_VISIBILITY_INLINES_HIDDEN     1)
        set(CMAKE_C_VISIBILITY_PRESET           hidden)    
        set(CMAKE_CXX_VISIBILITY_PRESET         hidden)            
        set(DEFINE_EXPORTS                      "-exported_symbols_list ${CMAKE_PROJECT_ROOT}/src/msix/exports.def")    
    ELSE()
        # on Linux and linux-derived platforms, you use a version script to achieve similar ends.
        SET(CMAKE_C_FLAGS                       "${CMAKE_C_FLAGS} -fvisibility=hidden")
        set(DEFINE_EXPORTS                      "-Wl,--version-script=${CMAKE_PROJECT_ROOT}/src/msix/symbol.map")
    ENDIF()
    MESSAGE(STATUS "Using export flag: ${DEFINE_EXPORTS}")
    set(CMAKE_SHARED_LINKER_FLAGS "${CMAKE_SHARED_LINKER_FLAGS} ${DEFINE_EXPORTS}")

    IF(OpenSSL_FOUND)
        MESSAGE (STATUS "Using OpenSSL ${OpenSLL_VERSION}")
        include_directories(
            ${include_directories}
            ${OpenSLL_INCLUDE_PATH}
        )
        set (SHA256    PAL/SHA256/OpenSSL/SHA256.cpp)
        set (Signature PAL/Signature/OpenSSL/SignatureValidator.cpp)
    ELSE()
        # ... and were done here...  :/
        MESSAGE (STATUS "OpenSSL NOT FOUND!")
        RETURN()
    ENDIF()

	set (DirectoryObject PAL/FileSystem/POSIX/DirectoryObject.cpp)
ENDIF()

MESSAGE (STATUS "PAL: DirectoryObject = ${DirectoryObject}")
MESSAGE (STATUS "PAL: SHA256          = ${SHA256}")
MESSAGE (STATUS "PAL: Signature       = ${Signature}")

# Create header for BlockMap schemas
file(READ "${CMAKE_PROJECT_ROOT}/AppxPackaging/BlockMap/schema/BlockMapSchema.xsd"     BLOCKMAP_SCHEMA)
file(READ "${CMAKE_PROJECT_ROOT}/AppxPackaging/BlockMap/schema/BlockMapSchema2015.xsd" BLOCKMAP_SCHEMA_2015)
file(READ "${CMAKE_PROJECT_ROOT}/AppxPackaging/BlockMap/schema/BlockMapSchema2017.xsd" BLOCKMAP_SCHEMA_2017)
set(BLOCKMAP_HEADER "// This file is generated by CMake and contains XSDs for parsing the AppxBlockMap.xml. Do not edit!!
#include <string>
#include <map>
std::map<std::string, std::string> blockMapSchema = {
    {\"blockMapSchemaRaw\",     R\"(${BLOCKMAP_SCHEMA})\"     },
    {\"blockMapSchema2015Raw\", R\"(${BLOCKMAP_SCHEMA_2015})\"},
    {\"blockMapSchema2017Raw\", R\"(${BLOCKMAP_SCHEMA_2017})\"}
    };
")
file(WRITE "../inc/AppxBlockMapSchemas.hpp" "${BLOCKMAP_HEADER}")

# Create header for [Content_Types] schema
file(READ "${CMAKE_PROJECT_ROOT}/AppxPackaging/[Content_Types]/opc-contentTypes.xsd"     CONTENT_TYPES_SCHEMA)
set(CONTENT_TYPES_HEADER "// This file is generated by CMake and contains XSDs for parsing [Content_Types].xml. Do not edit!!
#include <string>
#include <map>
std::map<std::string, std::string> contentTypesSchema = {
    {\"contentTypesSchemaRaw\",     R\"###(${CONTENT_TYPES_SCHEMA})###\"  }
    };
")
file(WRITE "../inc/ContentTypesSchemas.hpp" "${CONTENT_TYPES_HEADER}")

set(LIB_PUBLIC_HEADERS
    ../inc/AppxPackaging.hpp
    ../inc/MSIXWindows.hpp
)

set(LIB_PRIVATE_HEADERS
    ../inc/AppxBlockMapObject.hpp
    ../inc/AppxFactory.hpp
    ../inc/AppxPackageObject.hpp
    ../inc/AppxSignature.hpp
    ../inc/ComHelper.hpp
    ../inc/DirectoryObject.hpp
    ../inc/Exceptions.hpp
    ../inc/FileStream.hpp
    ../inc/InflateStream.hpp
    ../inc/Log.hpp
    ../inc/ObjectBase.hpp
    ../inc/RangeStream.hpp
    ../inc/StorageObject.hpp
    ../inc/StreamBase.hpp
    ../inc/UnicodeConversion.hpp
    ../inc/VectorStream.hpp
    ../inc/VerifierObject.hpp
    ../inc/XmlObject.hpp
    ../inc/ZipFileStream.hpp
    ../inc/ZipObject.hpp
)

set(LIB_SOURCES
    AppxBlockMapObject.cpp
    AppxFactory.cpp
    AppxPackageObject.cpp
    AppxPackaging_i.cpp
    AppxSignature.cpp
    InflateStream.cpp
    Log.cpp
    UnicodeConversion.cpp
    msix.cpp
    ZipObject.cpp
    ${DirectoryObject}
    ${SHA256}
    ${Signature}
)

# Copy out public headers
 configure_file(../inc/MSIXWindows.hpp   ${CMAKE_CURRENT_BINARY_DIR}/MSIXWindows.hpp  )
 configure_file(../inc/AppxPackaging.hpp ${CMAKE_CURRENT_BINARY_DIR}/AppxPackaging.hpp)

# Define the library
add_library(${LIBRARY_NAME} SHARED ${LIB_SOURCES} ${LIB_PUBLIC_HEADERS} ${LIB_PRIVATE_HEADERS})

# specify that this library is to be built with C++14
set_property(TARGET ${LIBRARY_NAME} PROPERTY CXX_STANDARD 14)

# Set the build version. It will be used in the name of the lib, with corresponding
# symlinks created. SOVERSION could also be specified for api version.
set_target_properties(${LIBRARY_NAME} PROPERTIES
	VERSION ${VERSION}  # ${VERSION} was defined in the main CMakeLists.
	FRAMEWORK FALSE
	PUBLIC_HEADER "${LIB_HEADERS}" # specify the public headers
)

MESSAGE(STATUS "MSIX takes a static dependency on zlib and xerces")
include_directories(
	${include_directories}
	${CMAKE_LIBRARY_OUTPUT_DIRECTORY}/zlib
	${CMAKE_PROJECT_ROOT}/lib/zlib
	${CMAKE_LIBRARY_OUTPUT_DIRECTORY}/xerces/src
	${CMAKE_PROJECT_ROOT}/lib/xerces/src
)

target_link_libraries(${PROJECT_NAME} PRIVATE zlibstatic)
target_link_libraries(${PROJECT_NAME} PRIVATE xerces-c)

IF(AOSP)
    target_link_libraries(${PROJECT_NAME} PRIVATE -latomic)
ENDIF()

IF(OpenSSL_FOUND)
    # include the libraries needed to use OpenSSL
    target_link_libraries(${PROJECT_NAME} PRIVATE crypto)
ENDIF()

if(WIN32)
    target_link_libraries(${PROJECT_NAME} PRIVATE bcrypt crypt32 wintrust)
endif()